LOAD "testlib.lgo

TO MIDITEST.RUNALREADYOPENTEST :instructions
  (RUNEXPECTERRORTEST :instructions 1301 "|MIDI Error: Already open|)
END

TO MIDITEST.RUNDEVICENOTOPENTEST :instructions
  (RUNEXPECTERRORTEST :instructions 1303 "|MIDI Error: Not open|)
END

TO MIDITEST.PLAYTUNE

   MIDIMESSAGE [144 75 127]
   WAIT 16
   MIDIMESSAGE [144 77 127]
   WAIT 16
   MIDIMESSAGE [128 75 0]
   WAIT 2
   MIDIMESSAGE [144 78 127]
   WAIT 16
   MIDIMESSAGE [128 77 0]
   WAIT 25
   MIDIMESSAGE [144 78 127]
   WAIT 40
   MIDIMESSAGE [144 78 127]
   WAIT 60
   MIDIMESSAGE [176 123 0]

END

TO MIDITEST.LONGFORMAT

   IGNORE MIDIOPEN

   ; use the long format to play the pan flute and gunshot together.
   MIDIMESSAGE [192 127 0 193 75 0]

   MIDIMESSAGE [144 75 127 145 75 127]
   WAIT 16
   MIDIMESSAGE [144 77 127 145 77 127]
   WAIT 16
   MIDIMESSAGE [128 75 0   129 75 0]
   WAIT 2
   MIDIMESSAGE [144 78 127 145 78 127]
   WAIT 16
   MIDIMESSAGE [128 77 0   129 77 0]
   WAIT 25
   MIDIMESSAGE [144 78 127 145 78 127]
   WAIT 40
   MIDIMESSAGE [144 78 127 145 78 127]
   WAIT 60
   MIDIMESSAGE [176 123 0  177 123 0]

   MIDICLOSE
END

TO MIDITEST.SMOKETEST

   IGNORE MIDIOPEN

   MIDIMESSAGE [192 0 0] ; use acoustic grand piano
   MIDITEST.PLAYTUNE

   MIDIMESSAGE [192 75 0] ; use pan flute
   MIDITEST.PLAYTUNE

   MIDIMESSAGE [192 127 0] ; use gunshot
   MIDITEST.PLAYTUNE
   
   MIDICLOSE

END

TO MIDITEST.DEVICEZERO

   IGNORE (MIDIOPEN 0)
   MIDITEST.PLAYTUNE
   MIDICLOSE

END

TO MIDITEST.BADINPUT

   ;
   ; bad input for MIDIOPEN
   ;
   
   ; open already opened
   TRACEINSTRUCTION            [IGNORE MIDIOPEN]
   MIDITEST.RUNALREADYOPENTEST [IGNORE MIDIOPEN]
   TRACEINSTRUCTION            [MIDICLOSE]

   ; bad input to midiopen shouldn't open the MIDI sequencer
   RUNDOESNTLIKEINPUTTEST [IGNORE (MIDIOPEN "badinput)]
   TRACEINSTRUCTION       [IGNORE MIDIOPEN]
   TRACEINSTRUCTION       [MIDICLOSE]

   ; bad input to midiopen shouldn't open the MIDI sequencer
   RUNDOESNTLIKEINPUTTEST [IGNORE (MIDIOPEN [1 2 3])]
   TRACEINSTRUCTION       [IGNORE MIDIOPEN]
   TRACEINSTRUCTION       [MIDICLOSE]

   ; bad input to midiopen shouldn't open the MIDI sequencer
   RUNDOESNTLIKEINPUTTEST [IGNORE (MIDIOPEN {1 2 3})]
   TRACEINSTRUCTION       [IGNORE MIDIOPEN]
   TRACEINSTRUCTION       [MIDICLOSE]

   ; bad input to midiopen shouldn't open the MIDI sequencer
   RUNDOESNTLIKEINPUTTEST [IGNORE (MIDIOPEN 500)]
   TRACEINSTRUCTION       [IGNORE MIDIOPEN]
   TRACEINSTRUCTION       [MIDICLOSE]

   RUNTOOMANYINPUTSTEST  [(MIDIOPEN 0 "toomany)]

   ;
   ; bad input for MIDICLOSE
   ;
   RUNTOOMANYINPUTSTEST  [(MIDICLOSE "toomany)]

   ; close unopened
   MIDITEST.RUNDEVICENOTOPENTEST [MIDICLOSE]

   ;
   ; bad input for MIDIMESSAGE
   ;
   TRACEINSTRUCTION [IGNORE MIDIOPEN]

   RUNNOTENOUGHINPUTSTEST  [(MIDIMESSAGE)]
   RUNTOOMANYINPUTSTEST    [(MIDIMESSAGE [176 123 0] "toomany)]

   RUNDOESNTLIKEINPUTTEST [MIDIMESSAGE "|176 123 0|  ]
   RUNDOESNTLIKEINPUTTEST [MIDIMESSAGE {176 123 0}   ]
   RUNDOESNTLIKEINPUTTEST [MIDIMESSAGE [176 123]     ]
   RUNDOESNTLIKEINPUTTEST [MIDIMESSAGE [176 123 0 0] ]

   RUNDOESNTLIKEINPUTTEST [MIDIMESSAGE [176 onetwothree 0] ]
   RUNDOESNTLIKEINPUTTEST [MIDIMESSAGE [176 [123]       0] ]
   RUNDOESNTLIKEINPUTTEST [MIDIMESSAGE [176 {123}       0] ]

   TRACEINSTRUCTION [MIDICLOSE]

END

TO MIDITEST

   MIDITEST.SMOKETEST
   MIDITEST.LONGFORMAT
   MIDITEST.DEVICEZERO
   MIDITEST.BADINPUT

END

LOAD "testlib.lgo

TO TESTSMALLWINDOWHANG

   ; This function undocks the commander, resizes the screen 
   ; window so that it is very small, then redocks the commander.
   ; This is a regression test for Bug #276, which caused an
   ; infinite regress in this condition.

   ; undock the commander
   TEXTSCREEN

   ; Load the Windows User API client DLL
   DLLLOAD "user32.dll

   ; Get Handle to Main Window (Note args are in REVERSE order)
   LOCALMAKE "hwnd DLLCALL [l FindWindowA S FMSLogo L 0]

   ; Shrink the Main Window
   LOCALMAKE "status DLLCALL (LIST "L "MoveWindow "W "1 "L "0 "L "0 "L "0 "L "0 "L :hwnd)

   ; redock the commander.  This is where FMSLogo used to hang.
   SPLITSCREEN

   ; Resize Main Window
   MAKE "status DLLCALL (LIST "L "MoveWindow "W "1 "L "400 "L "400 "L "0 "L "0 "L :hwnd)

   ; cleanup
   DLLFREE

   REPORTTESTPASSED [Resizing FMSLogo to be very small didn't cause an infinite loop.]
END
